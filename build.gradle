plugins {
	id 'java'
	id 'idea'
	id 'org.springframework.boot' version '3.5.4'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.siso'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {

    // AWS SDK v2 BOM (버전은 예시)
    implementation platform("software.amazon.awssdk:bom:2.25.50")
    implementation("io.awspring.cloud:spring-cloud-aws-starter-s3:3.0.2")

    // S3 클라이언트
    implementation "software.amazon.awssdk:s3"

    // URLConnection 기반 HTTP 클라이언트 (현재 코드에서 사용)
    implementation "software.amazon.awssdk:url-connection-client"

	// 드라이버는 둘 다 runtimeOnly (스프링이 URL로 자동 선택)

    implementation 'org.mariadb.jdbc:mariadb-java-client:3.4.1' // 예시 버전

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-validation' // Jakarta Validation 지원
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'

    // Agora RTC Token 생성용 라이브러리
    implementation("io.agora:authentication:2.1.3")

	// jwt
	implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.2'
	runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.2'
	runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.11.2'

	// swagger
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'

	// 오디오 파일
	implementation 'com.mpatric:mp3agic:0.9.1' // MP3 파일 메타데이터 추출용
	implementation 'org.gagravarr:vorbis-java-core:0.8' // OGG 파일 지원용
	implementation 'ws.schild:jave-all-deps:3.3.1' // 모든 오디오 형식 지원 (FFmpeg 기반)

	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'

	// 테스트 프레임워크
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// Testcontainers (실제 MySQL 컨테이너로 테스트)
	testImplementation 'org.testcontainers:testcontainers:1.19.3'
	testImplementation 'org.testcontainers:junit-jupiter:1.19.3'
	testImplementation 'org.testcontainers:mysql:1.19.3'

	// AssertJ (유창한 assertion)
	testImplementation 'org.assertj:assertj-core:3.24.2'

	// MockWebServer (외부 API 모킹 - OAuth, FCM 등)
	testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'

	// Awaitility (비동기 테스트)
	testImplementation 'org.awaitility:awaitility:4.2.0'

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

    //websocket
    implementation 'org.springframework.boot:spring-boot-starter-websocket'

    // Firebase Admin SDK
    implementation 'com.google.firebase:firebase-admin:9.4.3'

    // RabbitMQ (메시지 큐)
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    testImplementation 'org.springframework.amqp:spring-rabbit-test'
    testImplementation 'org.testcontainers:rabbitmq:1.19.3'

    // Redis (캐싱 및 세션)
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // Caffeine Cache (로컬 캐싱)
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'

    // QueryDSL (동적 쿼리)
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    // 구조화된 로깅 (Logstash Encoder for Logback)
    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'
}

// ===== Source Sets =====
sourceSets {
	testUnit {
		java.srcDir 'src/test_unit/java'
		resources.srcDir 'src/test/resources'
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
	testIntegration {
		java.srcDir 'src/test_integration/java'
		resources.srcDir 'src/test/resources'
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}

// annotation processor 출력을 build/ 하위로 지정
tasks.named('compileTestUnitJava') {
	options.generatedSourceOutputDirectory.set(file("${buildDir}/generated/sources/annotationProcessor/java/testUnit"))
}
tasks.named('compileTestIntegrationJava') {
	options.generatedSourceOutputDirectory.set(file("${buildDir}/generated/sources/annotationProcessor/java/testIntegration"))
}

// ===== Configurations =====
configurations {
	testUnitImplementation.extendsFrom testImplementation
	testUnitRuntimeOnly.extendsFrom testRuntimeOnly
	testUnitCompileOnly.extendsFrom compileOnly
	testUnitAnnotationProcessor.extendsFrom annotationProcessor

	testIntegrationImplementation.extendsFrom testImplementation
	testIntegrationRuntimeOnly.extendsFrom testRuntimeOnly
	testIntegrationCompileOnly.extendsFrom compileOnly
	testIntegrationAnnotationProcessor.extendsFrom annotationProcessor
}

// ===== Test Tasks =====
tasks.named('test') {
	useJUnitPlatform()
}

tasks.register('unitTest', Test) {
	description = 'Run unit tests'
	group = 'verification'
	testClassesDirs = sourceSets.testUnit.output.classesDirs
	classpath = sourceSets.testUnit.runtimeClasspath
	useJUnitPlatform()
}

tasks.register('integrationTest', Test) {
	description = 'Run integration tests'
	group = 'verification'
	testClassesDirs = sourceSets.testIntegration.output.classesDirs
	classpath = sourceSets.testIntegration.runtimeClasspath
	useJUnitPlatform()
}

tasks.named('check') {
	dependsOn tasks.named('unitTest')
	dependsOn tasks.named('integrationTest')
}

/* =======================
   IntelliJ IDEA 커스텀 소스셋 인식
 ======================= */
idea {
	module {
		testSources.from(sourceSets.testUnit.java.srcDirs)
		testSources.from(sourceSets.testIntegration.java.srcDirs)
		testResources.from(sourceSets.testUnit.resources.srcDirs)
		testResources.from(sourceSets.testIntegration.resources.srcDirs)
	}
}